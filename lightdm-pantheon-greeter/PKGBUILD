# Maintainer: sh4nks
# Contributor: faidoc, Ner0, M0Rf30

pkgname=lightdm-pantheon-greeter-bzr
pkgver=138
pkgrel=1
pkgdesc="LightDM greeter from the ElementaryOS team written in Vala "
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-greeter"
license=('GPL')
groups=('lightdm')
depends=('granite-bzr' 'libindicator3' 'clutter-gtk')
makedepends=('bzr' 'cmake' 'vala' 'pkg-config')
provides=('pantheon-greeter')
conflicts=('lightdm-pantheon-greeter')
install=${pkgname}.install

_bzrtrunk=lp:pantheon-greeter
_bzrmod=pantheon-greeter


build(){
  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  [ -d "$srcdir/$_bzrmod-build" ] && rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  sed -i 's/elementaryos-default/pantheon-greeter/g' data/org.pantheon.desktop.greeter.gschema.xml
  sed -i 's|${DEPS_LIBRARIES}|${DEPS_LIBRARIES} -lm|g' CMakeLists.txt

  [ -d build ] && rm -rf build
  mkdir build
  cd build
  CFLAGS="$CFLAGS -lm -Wno-deprecated-declarations"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make ${MAKEFLAGS}
}

package(){
  cd "$srcdir/$_bzrmod-build/build"

  make DESTDIR="${pkgdir}" install
}
